group 'io.puharesource.simplemavenrepo'
version '1.0.0'

buildscript {
    ext.kotlin_version = '1.7.10'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8

final main = 'io.puharesource.simplemavenrepo.SimpleMavenRepoKt'

application {
    mainClass = main
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'A Simple Maven Repository',
                'Implementation-Version': version,
                'Main-Class': main
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

project.tasks.assemble.dependsOn project.tasks.fatJar
jar.enabled = false

clean.doFirst {
    delete "$rootDir/out/"
}

tasks.withType(Jar) {
    destinationDir = file("$rootDir/out/")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-html-jvm', version: '0.8.0'

    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.11'

    implementation group: 'com.sparkjava', name: 'spark-kotlin', version: '1.0.0-alpha'
    implementation group: 'com.qmetric', name: 'spark-authentication', version: '1.4'

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    implementation group: 'com.github.salomonbrys.kotson', name: 'kotson', version: '2.5.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.5'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
